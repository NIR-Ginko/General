#----------------------------------------------------------------------------
# Copyright (C) 2002 ООО "НИП-Информатика"
#----------------------------------------------------------------------------
# АВТОР     Юрий Романченко
# АВТОР     Игорь Чудов <nir@nir.org.ru>
# СОЗДАНО   5 сентября 2002 г.
# ИЗМЕНЕНО  13.05.2015
# ОПИСАНИЕ  Генератор документа "Спецификация карты раскроя"
#           в формате RTF
#----------------------------------------------------------------------------
# СПИСОК ИЗМЕНЕНИЙ
# YVR20130618: Эскиз карты
#----------------------------------------------------------------------------

*'..\NestVars.inc'
*'RTF.inc'

#----------------------------------------------------------------------------
# Стандартное начало RTF
#----------------------------------------------------------------------------

ВЫЗОВ rtfНачФайла
ВЫЗОВ rtfШрифты
ВЫЗОВ rtfСтили
# Делаем маленький отступ сверху. Единица измерения - 1/1440 дюйма.
ВЫВОДСТР '\margt250'
# Делаем маленький отступ снизу. Единица измерения - 1/1440 дюйма.
ВЫВОДСТР '\margb250'
# Делаем небольшие отступы слева и справа.
ВЫВОДСТР '\margl350'
ВЫВОДСТР '\margr350'

#----------------------------------------------------------------------------
# Заголовок документа
#----------------------------------------------------------------------------

ВЫЗОВ rtfСтиль( 's'='sTITLE' )
ВЫВОДСТР 'Карта раскроя', PAR

#----------------------------------------------------------------------------
# Основные сведения
#----------------------------------------------------------------------------

ВЫЗОВ rtfСтиль( 's'='sFIELD' )
ВЫВОДСТР 'Задание:',        TAB, КодЗадание, PAR
ВЫВОДСТР 'Карта раскроя:',  TAB, КодЛиста, PAR
ВЫВОДСТР 'Количество:',     TAB, ЧислоЛистов, PAR
ВЫВОДСТР 'Лист:', TAB, МатЛиста , ' ' , ДлинаЛиста, ' x ', ШиринаЛиста, ' x ', ТолщЛиста, PAR
ВЫВОДСТР 'Разработчик:',    TAB, ВладЛиста, '/____________/____________',PAR
ВЫВОДСТР 'Дата создания:',  TAB, ДатаСозЛиста, PAR
ВЫВОДСТР 'Дата изменения:', TAB, ДатаИзмЛиста, PAR
#ВЫВОДСТР 'Комментарий: ' , TAB , ПарамЗадания , PAR
ВЫВОДСТР 'Комментарий: _____________________________________________________________________________________________________' , PAR
ВЫВОДСТР '__________________________________________________________________________________________________________________' , PAR
ВЫВОДСТР '__________________________________________________________________________________________________________________' , PAR
ВЫВОДСТР '__________________________________________________________________________________________________________________' , PAR

#----------------------------------------------------------------------------
# Эскиз
#----------------------------------------------------------------------------

ВЫЗОВ rtfСтиль( 's'='sFIELD' )
#ВЫВОДСТР 'Эскиз раскроя:' , PAR

# Организовываем строчку-пробел перед эскизом.
ВЫВОДСТР ' ' , PAR

ВЫВОДСТР '{\*\shppict{\pict\emfblip'
# Поворот: °ПОЧС * 65536
#ВЫВОДСТР '{\*\picprop{\sp{\sn rotation}{\sv -5898240}}}' # поворот 90°ПРЧС
ВЕЩ wEMF = 200 * MM2TWIPS # желаемая ширина эскиза
ВЕЩ hEMF = wEMF * ШиринаЛиста / ДлинаЛиста
ВЫВОДСТР '\picwgoal',wEMF,'\pichgoal',hEMF

# Первые 4 строки были %BITSHL вместо +BITSHL.
# Подозреваю, что это подобие комментирования и раскомментирования функции.

СТРОКА strEMF = КОДИРОВ(ЭСКИЗ(0 %
	+BITSHL(1,0) % DRAW_POLY
	+BITSHL(1,1) % DRAW_TRAC
	+BITSHL(1,2) % DRAW_CONT
	+BITSHL(1,3) % DRAW_HTRAC
	+BITSHL(1,4) % DRAW_PART
	+BITSHL(1,5) % DRAW_BLANK
	+BITSHL(1,6) % DRAW_MARK
	+BITSHL(1,7) % DRAW_SCRAP
	+BITSHL(1,8) % DRAW_DESIGN
	+BITSHL(1,9) % DRAW_TTF
	+BITSHL(1,10)% DRAW_SHX
	))
ВЕЩ endEMF = ДЛИНАСТР(strEMF)
ВЕЩ begEMF = 1
ВЕЩ lenEMF = 80
ПОВТОР ПОКА ( endEMF > 0 )
	ЕСЛИ ( lenEMF > endEMF ) lenEMF = endEMF
	ВЫВОДСТР ПОДСТР( strEMF, begEMF, lenEMF )
	endEMF = endEMF - lenEMF
	begEMF = begEMF + lenEMF
КОНЦИКЛ

ВЫВОДСТР '}}'

# Организовываем строчку-пробел перед таблицей.
ВЫВОДСТР ' ' , PAR

#----------------------------------------------------------------------------
# Таблица ДЕТАЛИ_НА_ЛИСТЕ
#----------------------------------------------------------------------------

ВЫВОДСТР PAR # без имени таблицы

ВЕЩ Ширины1(7)
Ширины1(1) = 10.0
Ширины1(2) = 70
Ширины1(3) = 16.0
Ширины1(4) = 15.0
Ширины1(5) = 22.0
Ширины1(6) = 16
Ширины1(7) = 50

ВЫЗОВ rtfШапка( 'W'='Ширины1', 'N'='7' )
ВЫВОДСТР '№', CEL
ВЫВОДСТР 'Обозначение детали', CEL
ВЫВОДСТР 'Колич.', CEL
ВЫВОДСТР 'Масса\line кг', CEL
ВЫВОДСТР 'Габариты\line мм', CEL
ВЫВОДСТР 'Норма', CEL
ВЫВОДСТР 'Комментарий' , CEL
ВЫВОДСТР ROW

ВЕЩ КолНаимен = 0
ВЕЩ ДетНаЛисте = 0
ВЕЩ МассаОбщ = 0
ВЕЩ ПлощОбщ = 0
ЛОГИЧ ЕстьФаски = ЛОЖЬ
ТАБЛИЦА 'ДЕТАЛИ_НА_ЛИСТЕ'
   ВЫЗОВ rtfТело( 'W'='Ширины1', 'N'='7' )
   ВЫВОДСТР НомерДет, CEL
   ВЫВОДСТР Обозначение, ' ' , ИмяДетали , CEL
   ВЫВОДСТР ЧислоДет, CEL
   ВЫВОДСТР МассаДет*ЧислоДет:'???#.#?', CEL
   ВЫВОДСТР ДлинаДет, ' x ', ШиринаДет, CEL
   ВЫВОДСТР МассаДет/КоэфИсп:'???#.#?', CEL
   ВЫВОДСТР КомДетали , CEL
   ВЫВОДСТР ROW
   ДетНаЛисте = ДетНаЛисте+ЧислоДет
   МассаОбщ = МассаОбщ+МассаДет*ЧислоДет
   ПлощОбщ = ПлощОбщ+ПлощДетали*ЧислоДет
   ТАБЛИЦА 'ФАСКИ_ДЕТАЛИ' Обозначение
      ЕстьФаски = ИСТИНА
   КОНТАБ
   КолНаимен = КолНаимен + 1
КОНТАБ

#----------------------------------------------------------------------------
# Таблица ФАСКИ_ДЕТАЛИ
#----------------------------------------------------------------------------

ЕСЛИ (ЕстьФаски) ТО

   ВЫЗОВ rtfСтиль( 's'='sTABNAME' )
   ВЫВОДСТР 'Разделка кромок', PAR

   ВЕЩ Ширины2(12)
   ВЕЩ Шир2 = 11.5
   Ширины2(1) = -10.0
   Ширины2(2) = -31.5
   Ширины2(3) = -11.5
   Ширины2(4) = 7.0
   Ширины2(5) = Шир2
   Ширины2(6) = Шир2
   Ширины2(7) = Шир2
   Ширины2(8) = Шир2
   Ширины2(9) = Шир2
   Ширины2(10) = Шир2
   Ширины2(11) = Шир2
   Ширины2(12) = Шир2

   ВЫЗОВ rtfШапка( 'W'='Ширины2', 'N'='12' )
   ВЫВОДСТР 'N п/п', CEL
   ВЫВОДСТР 'Обозначение детали', CEL
   ВЫВОДСТР 'Кол.\line дет.', CEL
   ВЫВОДСТР 'Ф', CEL
   ВЫВОДСТР 'УЛ1', CEL
   ВЫВОДСТР 'КЛ1', CEL
   ВЫВОДСТР 'УЛ2', CEL
   ВЫВОДСТР 'КЛ2', CEL
   ВЫВОДСТР 'УТ1', CEL
   ВЫВОДСТР 'КТ1', CEL
   ВЫВОДСТР 'УТ2', CEL
   ВЫВОДСТР 'КТ2', CEL
   ВЫВОДСТР ROW

   ТАБЛИЦА 'ДЕТАЛИ_НА_ЛИСТЕ'
      ЕстьФаски = ЛОЖЬ
      ТАБЛИЦА 'ФАСКИ_ДЕТАЛИ' Обозначение
         ЕстьФаски = ИСТИНА
      КОНТАБ
      ЕСЛИ (ЕстьФаски) ТО
         ВЕЩ ПерваяСтрока = TRUE
         ТАБЛИЦА 'ФАСКИ_ДЕТАЛИ' Обозначение
            ВЫЗОВ rtfТело( 'W'='Ширины2', 'N'='12', 'first'='ПерваяСтрока' )
            ВЫВОДСТР (ПерваяСтрока = TRUE) ? СТРОКА(НомерДет) : '', CEL
            ВЫВОДСТР (ПерваяСтрока = TRUE) ? СТРОКА(Обозначение) : '', CEL
            ВЫВОДСТР (ПерваяСтрока = TRUE) ? СТРОКА(ЧислоДет) : '', CEL
            ВЫВОДСТР ИмяФаски, CEL
            ВЫВОДСТР УголЛицНач:'?#V?', CEL
            ВЫВОДСТР ВелЛицНач:'?#V?', CEL
            ВЫВОДСТР УголЛицКон:'?#V?', CEL
            ВЫВОДСТР ВелЛицКон:'?#V?', CEL
            ВЫВОДСТР УголТылНач:'?#V?', CEL
            ВЫВОДСТР ВелТылНач:'?#V?', CEL
            ВЫВОДСТР УголТылКон:'?#V?', CEL
            ВЫВОДСТР ВелТылКон:'?#V?', CEL
            ВЫВОДСТР ROW
            ПерваяСтрока = FALSE
         КОНТАБ
      КОНЕСЛИ
   КОНТАБ

КОНЕСЛИ

#----------------------------------------------------------------------------
# Таблица ДЕЛОВОЙ_ОТХОД
#----------------------------------------------------------------------------

ЛОГИЧ ЕстьОтход = ЛОЖЬ
ТАБЛИЦА 'ДЕЛОВОЙ_ОТХОД'
   ЕстьОтход = ИСТИНА
КОНТАБ
ЕСЛИ (ЕстьОтход) ТО

   ВЫЗОВ rtfСтиль( 's'='sTABNAME' )
   ВЫВОДСТР 'Деловой отход', PAR

   ВЕЩ Ширины3(4)
   Ширины3(1) = 10.0
   Ширины3(2) = 22.0
   Ширины3(3) = 20.0
   Ширины3(4) = 15.0

   ВЫЗОВ rtfШапка( 'W'='Ширины3', 'N'='4' )
   ВЫВОДСТР 'N п/п', CEL
   ВЫВОДСТР 'Габариты\line мм', CEL
   ВЫВОДСТР 'Площадь\line м{\super 2}', CEL
   ВЫВОДСТР 'Масса\line кг', CEL
   ВЫВОДСТР ROW

   ТАБЛИЦА 'ДЕЛОВОЙ_ОТХОД'
      ВЫЗОВ rtfТело( 'W'='Ширины3', 'N'='4' )
      ВЫВОДСТР НомерСтроки, CEL
      ВЫВОДСТР ДлинаОст, ' x ', ШиринаОст, CEL
      ВЫВОДСТР ПлощадьОст:'?#.#?', CEL
      ВЫВОДСТР МассаОст:'???#.#?', CEL
      ВЫВОДСТР ROW
   КОНТАБ
КОНЕСЛИ

#----------------------------------------------------------------------------
# Таблица ИНСТРУМЕНТЫ
#----------------------------------------------------------------------------
ВЫЗОВ rtfСтиль( 's'='sFIELD' )
# Организовываем строчку-пробел перед новой таблицей.

ВЫВОДСТР PAR # без имени таблицы

# Ширины второй строки заголовка и строк тела таблицы
# Вертикальное объединение строк производится только в заголовке
ВЕЩ Ширины(6)
Ширины(1) = -82
Ширины(2) = 25.0
Ширины(3) = 25.0
Ширины(4) = 25.0
Ширины(5) = 25.0
Ширины(6) = -17.0

# Ширины первой строки заголовка
ВЕЩ ШириныЗаг(4)
ШириныЗаг(1) = Ширины(1)
ШириныЗаг(2) = Ширины(2) + Ширины(3)
ШириныЗаг(3) = Ширины(4) + Ширины(5)
ШириныЗаг(4) = Ширины(6)

# Первая строка заголовка таблицы
ВЫЗОВ rtfШапка( 'W'='ШириныЗаг', 'N'='4', 'first'='TRUE', 'bB'='THIN' )
ВЫВОДСТР 'Инструмент', CEL
ВЫВОДСТР 'Рабочий ход', CEL
ВЫВОДСТР 'Холостой ход', CEL
ВЫВОДСТР 'Подача\line мм/мин', CEL
ВЫВОДСТР ROW

# Вторая строка заголовка таблицы
ВЫЗОВ rtfШапка( 'W'='Ширины', 'N'='6', 'first'='FALSE', 'bT'='THIN' )
ВЫВОДСТР CEL
ВЫВОДСТР 'Длина, мм', CEL
ВЫВОДСТР 'Время, час', CEL
ВЫВОДСТР 'Длина, мм', CEL
ВЫВОДСТР 'Время, час', CEL
ВЫВОДСТР CEL
ВЫВОДСТР ROW

ВЕЩ ПоследнИнструмент = 0
ТАБЛИЦА 'ИНСТРУМЕНТЫ'
   ЕСЛИ (ВремяРабоч<>0) ПоследнИнструмент = НомерСтроки
КОНТАБ
ВЕЩ ЧислоПробивок = 0
СТРОКА ИмяИнст(4)
ИмяИнст(1) = 'Резак средний'
ИмяИнст(2) = 'Резак левый  '
ИмяИнст(3) = 'Резак правый '
ИмяИнст(4) = 'Маркер       '

ТАБЛИЦА 'ИНСТРУМЕНТЫ'
   ЕСЛИ (ВремяРабоч<>0) ТО
      ЕСЛИ (НомерСтроки=1) ЧислоПробивок = ЧислоВкл
      ВЫЗОВ rtfТело( 'W'='Ширины', 'N'='6', %
	                 'bB'='(НомерСтроки=ПоследнИнструмент)?FAT:THIN' )
      ВЫВОДСТР ИмяИнст(НомерСтроки), CEL
	  ВЫВОДСТР ДлинРабоч:'????#.##', CEL
      ВЫЗОВ ВРЕМЯ_Ч(Вр=ВремяРабоч)
      ВЫВОДСТР ВремяСтр, CEL
      ВЫВОДСТР ДлинБыстро:'????#.##', CEL
      ВЫЗОВ ВРЕМЯ_Ч(Вр=ВремяБыстро)
      ВЫВОДСТР ВремяСтр, CEL
      ВЫВОДСТР ПодачаМин:'?????#', CEL
      ВЫВОДСТР ROW
   КОНЕСЛИ
КОНТАБ

#----------------------------------------------------------------------------
# Суммарные сведения
#----------------------------------------------------------------------------

ВЫЗОВ rtfСтиль( 's'='sFIELD' )
ВЫЗОВ ВРЕМЯ_Ч(Вр=ВремяОбщ)
ВЫВОДСТР PAR
ВЫВОДСТР 'Общее время обработки:', TAB, ВремяСтр, PAR
ВЫВОДСТР 'Размер УП:',             TAB, РазмерУП, PAR
ВЫВОДСТР 'Количество пробивок:',   TAB, ЧислоПробивок, PAR
ВЫВОДСТР 'Деталей на листе/наименований:',  TAB, ДетНаЛисте, '/' , КолНаимен , PAR
ВЫВОДСТР 'Плошадь листа/деталей, м{\super 2}:',   TAB, ПлощЛиста:'?#.#?', '/' , ПлощОбщ:'?#.#?' , PAR
ВЫВОДСТР 'Масса деталей, кг:',     TAB, МассаОбщ:'???#.#?', PAR
ВЫВОДСТР 'КИМ:',                   TAB, КоэфИсп:'#.###', PAR
ВЫВОДСТР 'Расход дроби на лист/детали, кг:', TAB , (ПлощЛиста*1.3):'??#.#?' , '/' , (ПлощОбщ*1.3):'??#.#?' , PAR

#----------------------------------------------------------------------------
# Стандартный конец RTF
#----------------------------------------------------------------------------

ВЫЗОВ rtfКонФайла
